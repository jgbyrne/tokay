# Factor is a consumable parselet either matching an Integer or another
# expression enclosed by brackets.
Factor : @{
    Integer             # Integer is a builtin matching 64-bit
                        # signed integer values from a stream
    '(' Expr ')'
}

# The next two parselets define sequences and their interpretation.
# Calculated values in sequences take highest precedence, therefore
# the calculate values become the result of each sequence.

Term : @{
    Term '*' Factor     $1 * $3
    Term '/' Factor     $1 / $3
    Factor
}

Expr : @{
    Expr '+' Term       $1 + $3
    Expr '-' Term       $1 - $3
    Term
}

# This is the main level, where an expression can occur.
# Any other input - by default - is ignored and skipped.
Expr
